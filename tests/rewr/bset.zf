# bset.zf

val [infix "*"] tup : type -> type -> type.
val set : type -> type.
val [infix "∈"] mem : pi (a : type). a -> set a -> prop.
val [infix ","] pair : pi (a b : type). a -> b -> (tup a b).
val [prefix "ℙ"] power : pi (a : type). set a -> set (set a).
val [infix "⊆"] subset : pi (a : type). set a -> set a -> prop.
val [infix "★"] times : pi (a b : type). set a -> set b -> set (tup a b).
val [infix "∪"] union : pi (a : type). set a -> set a -> set a.
val [infix "∩"] inter : pi (a : type). set a -> set a -> set a.
val [infix "-"] diff : pi (a : type). set a -> set a -> set a.
val [prefix "Ø"] empty: pi (a : type). set a.
val [infix "+->"] plmngt : pi (a b : type). set a -> set b -> set (set (tup a b)).
val [infix "-->"] mnmngt : pi (a b : type). set a -> set b -> set (set (tup a b)).
val relation: pi (a b : type). set a -> set b -> set (set (tup a b)).
val dom : pi (a b : type). set (tup a b) -> set a.
val [prefix "Img"] ran: pi (a b: type). set(tup a b) -> set b.
val inverse : pi (a b : type). set (tup a b) -> set (tup b a).
val [infix ";"] semicolon : pi (a b c : type). set (tup a b) -> set (tup b c) -> set (tup a c).
val image : pi (a b : type). set (tup a b) -> set a -> set b.
val id : pi (a : type). set a -> set (tup a a).
val [infix "◁"] domain_restriction : pi (a b : type). set a -> set (tup a b) -> set (tup a b).
val [infix "▷"] range_restriction : pi (a b : type). set (tup a b) -> set b -> set (tup a b).
val [infix "⩤"]domain_substraction : pi (a b : type). set a -> set (tup a b) -> set (tup a b).
val [infix "⩥"] range_substraction : pi (a b : type). set (tup a b) -> set b -> set (tup a b).
val lspl : pi (a b : type). set (tup a b) -> set (tup a b) -> set (tup a b).
val direct_product : pi (a b c : type). set (tup a b) -> set (tup a c) -> set (tup a (tup b c)).
val parallel_product : pi (a b c d : type). set (tup a b) ->  set (tup c d) -> set (tup (tup a c) (tup b d)).
val singleton : pi (a : type). a -> set a.
val [infix "+->>"] plmngtgt : pi (a b : type). set a -> set b -> set (set (tup a b)).
val [infix ">->>"] gtmngtgt : pi (a b : type). set a -> set b -> set (set (tup a b)).
val [infix ">->"] gtmngt : pi (a b : type). set a -> set b -> set (set(tup a b)).
val [infix "-->>"] mnmngtgt : pi (a b : type). set a -> set b -> set (set(tup a b)).
val [infix ">+>"] gtplgt : pi (a b : type). set a -> set b -> set (set(tup a b)).
val [prefix "π₁"] prj1 : pi (a b : type). tup (set a) (set b) -> set (tup (tup a b) a).
val [prefix "π₂"] prj2 : pi (a b : type). tup (set a) (set b) -> set (tup (tup a b) b).

val [prefix "fst"] fst : pi (a b : type). tup a b -> a.
val [prefix "snd"] snd : pi (a b : type). tup a b -> b.

rewrite forall (a b : type) (x:a)(y:b). fst (pair _ _ x y) = x.
rewrite forall (a b : type) (x:a)(y:b). snd (pair _ _ x y) = y.

# every element of `tup a b` is a pair
rewrite forall (a b:type) (P:tup a b). pair a b (fst P) (snd P) = P.

#rewrite
#  forall (a b : type) (z : tup a b) (S : set a) (T : set b).
#    mem (tup a b) z (times a b S T) <=>
#      (exists (x : a) (y : b). z = (pair a b x y) && mem a x S && mem b y T).

rewrite
  forall (a : type) (b : type) (x : a) (y : b) (S : set a) (T : set b).
    mem (tup a b) (pair a b x y) (times a b S T) <=> mem a x S && mem b y T.

rewrite
  forall (a b : type) (z : tup a b) (S1 : set a) (S2 : set b).
    mem (tup a b) z (times _ _ S1 S2) <=>
      mem a (fst _ _ z) S1 && mem b (snd _ _ z) S2.

rewrite
  forall (a : type) (S T : set a).
    mem (set a) S (power a T) <=> (forall (x : a). (mem a x S) => (mem a x T)).

rewrite
  forall (a : type) (S T : set a). subset a S T <=> mem (set a) S (power a T).

# Extensional equality
rewrite
  forall (a : type) (S T : set a).
    S = T <=> (forall (x : a). (mem a x S) <=> (mem a x T)).

# Equality over tuples
rewrite
  forall (a b : type) (x y : tup a b).
    x = y <=> (fst a b x) = (fst a b y) && (snd a b x) = (snd a b y). 

rewrite
  forall (a : type) (x : a) (S T : set a).
    mem a x (union a S T) <=> mem a x S || mem a x T.

rewrite
  forall (a : type) (x : a) (S T : set a).
    mem a x (inter a S T) <=> mem a x S && mem a x T.
#diff
rewrite
  forall (a : type) (x : a) (S T : set a).
    mem a x (diff a S T) <=> mem a x S && ~ (mem a x T).

rewrite
  forall (a b : type) (x x1 : a) (y y1 : b).
    pair a b x y = pair a b x1 y1 <=> x = x1 && y = y1.

#empty
rewrite
  forall (a : type) (x : a).
    mem a x (empty a) <=> false.

#+-> PLusMiNusGreaTer plmngt mem_partial_function_set
#rewrite
#  forall (a b : type) (S : set a) (T : set b) (F : set (tup a b)).
#    mem (set (tup a b)) F (plmngt a b S T) <=> mem (set (tup a b)) F (relation a b S T) && (forall (X : a) (Y1 Y2 : b). mem (tup a b) (pair X Y1) F && (mem (tup a b) (pair X Y2) F) => (Y1 = Y2)).

rewrite
  forall (a b : type) (S : set a) (T : set b) (F : set (tup a b)).
    mem (set (tup a b)) F (plmngt a b S T) <=> (forall (X : a) (Y1 Y2 : b). mem (tup a b) (pair X Y1) F && (mem (tup a b) (pair X Y2) F) => (Y1 = Y2)).


#--> MiNusMiNusGreaTer mnmngt mem_total_function_set
rewrite
  forall (a b : type) (S : set a) (T : set b) (X : set (tup a b)).
    mem (set(tup a b)) X (mnmngt a b S T) <=> mem (set(tup a b)) X (plmngt a b S T) && (dom a b X) = S.


#ran
rewrite
  forall (a b : type) (P : set (tup a b)) (x : b).
    mem b x (ran a b P) <=> (exists (A1 : a). mem (tup a b) (pair a b A1 x) P).

#dom
rewrite
  forall (a b : type) (P : set (tup a b)) (x: a).
    mem a x (dom a b P) <=> (exists (b1 : b). mem (tup a b) (pair a b x b1) P).

#inverse
rewrite
  forall (a b : type) (P : set (tup a b)) (T: tup b a).
    mem (tup b a) T (inverse a b P) <=> mem (tup a b) (pair (snd T) (fst T)) P.
#rewrite
#  forall (a b : type) (P : set (tup a b)) (x : b) (y : a).
#    mem (tup b a) (pair b a x y) (inverse a b P) <=> mem (tup a b) (pair a b y x) P.

#relation
rewrite
   forall (a b : type) (U : set a) (V : set b) (R: set (tup a b)).
     mem (set (tup a b)) R (relation a b U V) <=>
      (forall (T: tup a b).
        mem (tup a b) T R => mem a (fst T) U && mem b (snd T) V).
#rewrite
#   forall (a b : type) (U : set a) (V : set b) (R: set (tup a b)).
#     mem (set (tup a b)) R (relation a b U V) <=>
#      (forall (x : a) (y : b).
#        mem (tup a b) (pair a b x y) R => mem a x U && mem b y V).

#semicolon
rewrite
  forall (a b c : type) (P : set (tup a b)) (Q : set (tup b c)) (T : (tup a c)).
    mem (tup a c) T (semicolon a b c P Q) <=>
    (exists (b1 : b). mem (tup a b) (pair a b (fst T) b1) P && mem (tup b c) (pair b c b1 (snd T)) Q).


#rewrite
#  forall (a b c : type) (P : set (tup a b)) (Q : set (tup b c)) (x : a) (y : c).
#    mem (tup a c) (pair a c x y) (semicolon a b c P Q) <=>
#    (exists (b1 : b). mem (tup a b) (pair a b x b1) P && mem (tup b c) (pair b c b1 y) Q).

#image
rewrite
  forall (a b : type) (P : set (tup a b)) (W : set a) (x : b).
    mem b x (image a b P W) <=> (exists (a1 : a). mem a a1 W && mem (tup a b) (pair a b a1 x) P).

#rewrite
#  forall (a b : type) (P : set (tup a b)) (W : set a) (x : b).
#    mem b x (image a b P W) <=> (exists (a1 : a). mem a a1 W && mem (tup a b) (pair a b a1 x) P).

#id
#rewrite
#  forall (a : type) (U : set a) (x : a) (y : a).
#    mem (tup a a) (pair a a x y) (id a U) <=> mem a x U && x = y.

#rewrite
#  forall (a : type) (U : set a) (X : a) (Y : a).
#    mem (tup a a) (pair a a X Y) (id a U) <=> mem a X U && X = Y.

rewrite
  forall (a : type) (U : set a) (z : tup a a).
    mem (tup a a) z (id a U) <=> mem a (fst a a z) U && (fst a a z) = (snd a a z).

#domain_restriction
rewrite
  forall (a b : type) (P: set (tup a b)) (S : set a) (T : tup a b).
    mem (tup a b) T (domain_restriction a b S P) <=>
      mem (tup a b) (pair (fst T) (snd T)) P && mem a (fst T) S.

#rewrite
#  forall (a b : type) (P: set (tup a b)) (S : set a) (x : a) (y : b).
#    mem (tup a b) (pair a b x y) (domain_restriction a b S P) <=> mem (tup a b) (pair a b x y) P && mem a x S.

#range_restriction
rewrite
  forall (a b : type) (P : set (tup a b)) (T : set b) (V: tup a b).
    mem (tup a b) V (range_restriction a b P T) <=> mem (tup a b) (pair (fst V) (snd V)) P && mem b (snd V) T.

#domain_substraction
rewrite
  forall (a b : type) (P : set (tup a b)) (S : set a) (T : tup a b).
    mem (tup a b) T (domain_substraction a b S P) <=> mem (tup a b) T P && ~ (mem a (fst T) S).

#range_substraction
rewrite
  forall (a b : type) (P : set (tup a b)) (T: set b) (V : tup a b).
    mem (tup a b) V (range_substraction a b P T) <=> mem (tup a b) V P && ~ (mem b (snd V) T).

#lspl mem_overriding
rewrite
  forall (a b : type) (P Q : set (tup a b)) (T : tup a b).
    mem (tup a b) T (lspl a b Q P) <=>
    mem (tup a b) T Q && ~ mem a (fst T) (dom a b P) || mem (tup a b) T P.

#rewrite
#  forall (a b : type) (P Q : set (tup a b)) (x : a) (y : b).
#    mem (tup a b) (pair a b x y) (lspl a b Q P) <=> mem (tup a b) (pair a b x y) Q && ~ mem a x (dom a b P) || mem (tup a b) (pair a b x y) P.

#direct_product
rewrite
  forall (a b c : type) (F : set (tup a b)) (G : set (tup a c)) (V : tup a (tup b c)).
    mem (tup a (tup b c)) V (direct_product a b c F G)
      <=> mem (tup a b) (pair (fst V) (fst (snd V))) F && mem (tup a c) (pair (fst V) (snd (snd V))) G.

#rewrite
#  forall (a b c : type) (F : set (tup a b)) (G : set (tup a c)) (x : a) (y : b) (z : c).
#    mem (tup a (tup b c)) (pair a (tup b c) x (pair b c y z)) (direct_product a b c F G)
#      <=> mem (tup a b) (pair a b x y) F && mem (tup a c) (pair a c x z) G.

#parallel_product
rewrite
  forall (a b c d : type) (H : set (tup a b)) (K : set (tup c d)) (T : tup (tup a c) (tup b d)).
    mem (tup (tup a c) (tup b d)) T (parallel_product a b c d H K) <=> mem (tup a b) (pair a b (fst (fst T)) (fst (snd T))) H && mem (tup c d) (pair c d (snd (fst T)) (snd (snd T))) K.

#rewrite
#  forall (a b c d : type) (H : set (tup a b)) (K : set (tup c d)) (x : a) (y: c) (z : b) (w : d).
#    mem (tup (tup a c) (tup b d)) (pair (tup a c) (tup b d) (pair a c x y) (pair b d z w)) (parallel_product a b c d H K) <=> mem (tup a b) (pair a b x z) H && mem (tup c d) (pair c d  y w) K.


#singleton
rewrite
  forall (a : type) (X Y : a).
    mem a X (singleton a Y) <=> X = Y.

#plmngtgt mem_partial_surjection_set
rewrite
  forall (a b : type) (S : set a) (T : set b) (X : set (tup a b)).
    mem (set (tup a b)) X (plmngtgt a b S T) <=> mem (set (tup a b)) X (plmngt a b S T) && ran a b X = T.

#gtmngtgt mem_total_bijection_set
rewrite
  forall (a b : type) (S : set a) (T : set b) (X : set (tup a b)).
    mem (set (tup a b)) X (gtmngtgt a b S T) <=> mem (set (tup a b)) X (gtmngt a b S T) && mem (set (tup a b)) X (mnmngtgt a b S T).

#gtmngt mem_total_injection_set
rewrite
  forall (a b : type) (S : set a) (T : set b) (X : set (tup a b)).
    mem (set (tup a b)) X (gtmngt a b S T) <=> mem (set (tup a b)) X (gtplgt a b S T) && mem (set (tup a b)) X (mnmngt a b S T).

#mnmngtgt mem_total_surjection_set
rewrite
  forall (a b : type) (S : set a) (T : set b) (X : set (tup a b)).
    mem (set (tup a b)) X (mnmngtgt a b S T) <=> mem (set (tup a b)) X (plmngtgt a b S T) && mem (set (tup a b)) X (mnmngt a b S T).

#gtplgt mem_partial_injection_set
rewrite
  forall (a b : type) (S : set a) (T : set b) (X : set (tup a b)).
    mem (set (tup a b)) X (gtplgt a b S T) <=> mem (set (tup a b)) X (plmngt a b S T) && mem (set (tup b a)) (inverse a b X) (plmngt b a T S).

#prj1
rewrite
  forall (a b : type) (S : set a) (T : set b) (V : tup (tup a b) a).
   mem (tup (tup a b) a) V (prj1 a b (pair (set a) (set b) S T))
   <=> mem (tup (tup a b) a) V (times (tup a b) a (times a b S T) S) && (snd V) = (fst (fst V)).

#rewrite
#  forall (a b : type) (S : set a) (T : set b) (x z : a) (y : b).
#   mem (tup (tup a b) a) (pair (tup a b) a (pair a b x y) z) (prj1 a b (pair (set a) (set b) S T))
#   <=> mem (tup (tup a b) a) (pair (tup a b) a (pair a b x y) z) (times (tup a b) a (times a b S T) S) && z = x.

#prj2
rewrite
  forall (a b : type) (S : set a) (T : set b) (V : tup (tup a b) b).
   mem (tup (tup a b) b) V (prj2 a b (pair (set a) (set b) S T))
   <=> mem (tup (tup a b) b) V (times (tup a b) b (times a b S T) T) && (snd V) = (snd (fst V)).

#rewrite
#  forall (a b : type) (S : set a) (T : set b) (x : a) (y z : b).
#   mem (tup (tup a b) b) (pair (tup a b) b (pair a b x y) z) (prj2 a b (pair (set a) (set b) S T))
#   <=> mem (tup (tup a b) b) (pair (tup a b) b (pair a b x y) z) (times (tup a b) b (times a b S T) T) && z = y.
